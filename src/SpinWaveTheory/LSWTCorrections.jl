"""
    energy_per_site_lswt_correction(swt::SpinWaveTheory; opts...)

Computes the [ùí™(1/Œª) or ùí™(1/S)] correction to the classical energy **per
site** [ùí™(Œª¬≤) or ùí™(S¬≤)] given a [`SpinWaveTheory`](@ref). The correction
[ùí™(Œª) or ùí™(S)] includes a uniform term (For instance, if the classical energy
is Œ±JS¬≤, the LSWT gives a correction like Œ±JS) and the summation over the
zero-point energy for all spin-wave modes, i.e., 1/2 ‚àë‚Çô ‚à´d¬≥q œâ(q, n), where q
belongs to the first magnetic Brillouin zone and n is the band index.

A keyword argument `rtol`, `atol`, or `maxevals` is required to control the
accuracy of momentum-space integration. See the HCubature package documentation
for details.
"""
function energy_per_site_lswt_correction(swt::SpinWaveTheory; opts...)
    any(in(keys(opts)), (:rtol, :atol, :maxevals)) || error("Must specify one of `rtol`, `atol`, or `maxevals` to control momentum-space integration.")

    (; sys) = swt
    Natoms = natoms(sys.crystal)
    L = nbands(swt)
    # Create matrix and vector buffers to reuse them
    H = zeros(ComplexF64, 2L, 2L)
    V = zeros(ComplexF64, 2L, 2L)
    E = zeros(L)

    if sys.mode == :SUN
        hamiltonian_function! = swt_hamiltonian_SUN!
    else
        @assert sys.mode in (:dipole, :dipole_large_S)
        hamiltonian_function! = swt_hamiltonian_dipole!
    end

    # The uniform term (trace of the (1,1)-block of the spin-wave Hamiltonian)
    q‚ÇÄ = Vec3(0.0, 0.0, 0.0)
    hamiltonian_function!(H, swt, q‚ÇÄ)
    Œ¥E = -real(tr(view(H, 1:L, 1:L))) / Natoms

    # Integrate zero-point energy over the first magnetic Brillouin zone ùê™ ‚àà [0, 1]¬≥ (in RLU)
    res = hcubature((0,0,0), (1,1,1); opts...) do q
        q = Vec3(q)
        # Clear the energy buffer
        E .= 0.0
        hamiltonian_function!(H, swt, q)
        E .= bogoliubov!(V, H)
        return sum(E) / 2Natoms
    end

    println("Zero-point energy is ", res[1], "¬±", res[2])

    Œ¥E += res[1]

    return Œ¥E

end

# Calculates the magnetization reduction for :SUN mode for site `i`
function magnetization_lswt_correction_sun(swt::SpinWaveTheory, i::Int; opts...)
    (; sys, data) = swt

    N = sys.Ns[1]
    Natoms = natoms(sys.crystal)
    L = (N - 1) * Natoms

    H = zeros(ComplexF64, 2L, 2L)
    V = zeros(ComplexF64, 2L, 2L)
    S_buf = zeros(ComplexF64, N, N)

    @views dipole = sys.dipoles[i]
    n = normalize(dipole)
    for Œº in 1:3
        @views O = data.observable_operators[:, :, Œº, i]
        @. S_buf += n[Œº] * O
    end

    res = hcubature((0,0,0), (1,1,1); opts...) do q
        q = Vec3(q)
        swt_hamiltonian_SUN!(H, swt, q)
        bogoliubov!(V, H)
        acc = 0.0
        for band in L+1:2L
            v = reshape(view(V, :, band), N-1, Natoms, 2)
            for Œ± in 1:N-1
                for Œ≤ in 1:N-1
                    acc += -(S_buf[N, N]*Œ¥(Œ±, Œ≤) - S_buf[Œ±, Œ≤]) * conj(v[Œ±, i, 1]) * v[Œ≤, i, 1]
                end
            end
        end
        return real(acc)
    end

    println("Site ", i)
    println("Classical magnetization")
    println(real(S_buf[N, N]))
    println("Correction from LSWT ")
    println(res[1], "¬±", res[2])
    return res[1]
end

# Calculates the magnetization reduction for :dipole mode for site `i`
function magnetization_lswt_correction_dipole(swt::SpinWaveTheory, i::Int; opts...)
    (; sys) = swt
    N = sys.Ns[1]
    S = (N-1)/2

    L = nbands(swt)
    H = zeros(ComplexF64, 2L, 2L)
    V = zeros(ComplexF64, 2L, 2L)

    res = hcubature((0,0,0), (1,1,1); opts...) do q
        swt_hamiltonian_dipole!(H, swt, Vec3(q))
        bogoliubov!(V, H)
        return -norm2(view(V, L+i, 1:L))
    end

    println("Site ", i)
    println("Classical magnetization")
    println(S)
    println("Correction from LSWT ")
    println(res[1], "¬±", res[2])

    return res[1]
end

"""
    magnetization_lswt_correction(swt::SpinWaveTheory, i::Int; opts...)

Calculates the reduction in the classical magnetization given a
[`SpinWaveTheory`](@ref) from LSWT for magnetic sublattice `i`. In the case of
`:dipole` and `:dipole_large_S` mode, the classical magnetization is always
maximized to spin size `S`. While in `:SUN` mode, the classical magnetization
can be smaller than `S` due to anisotropic interactions.

A keyword argument `rtol`, `atol`, or `maxevals` is required to control the
accuracy of momentum-space integration. See the HCubature package documentation
for details.
"""
function magnetization_lswt_correction(swt::SpinWaveTheory, i::Int; opts...)
    any(in(keys(opts)), (:rtol, :atol, :maxevals)) || error("Must specify one of `rtol`, `atol`, or `maxevals` to control momentum-space integration.")

    (; sys) = swt
    if sys.mode == :SUN
        Œ¥S = magnetization_lswt_correction_sun(swt, i; opts...)
    else
        @assert sys.mode in (:dipole, :dipole_large_S)
        Œ¥S = magnetization_lswt_correction_dipole(swt, i; opts...)
    end
    return Œ¥S
end
