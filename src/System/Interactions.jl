function empty_interactions(na, N)
    return map(1:na) do _
        Interactions(empty_anisotropy(N),
                     Coupling{Float64}[],
                     Coupling{Mat3}[],
                     Coupling{Float64}[])
    end
end

# Creates a clone of the lists of exchange interactions, which can be mutably
# updated.
function clone_interactions(ints::Interactions)
    (; aniso, heisen, exchange, biquad) = ints
    return Interactions(aniso, copy(heisen), copy(exchange), copy(biquad))
end

function interactions_homog(sys::System{N}) where N
    return sys.interactions_union :: Vector{Interactions}
end

function interactions_inhomog(sys::System{N}) where N
    return sys.interactions_union :: Array{Interactions, 4}
end

function is_homogeneous(sys::System{N}) where N
    return sys.interactions_union isa Vector{Interactions}
end

"""
    to_inhomogeneous(sys::System)

Returns a copy of the system that allows for inhomogeneous interactions, which
can be set using [`set_anisotropy_at!`](@ref), [`set_exchange_at!`](@ref),
[`set_biquadratic_at!`](@ref), and [`set_vacancy_at!`](@ref).

Inhomogeneous systems do not support symmetry-propagation of interactions or
system reshaping.
"""
function to_inhomogeneous(sys::System{N}) where N
    is_homogeneous(sys) || error("System is already inhomogeneous.")
    ints = interactions_homog(sys)

    ret = clone_system(sys)
    na = natoms(ret.crystal)
    ret.interactions_union = Array{Interactions}(undef, ret.latsize..., na)
    for i in 1:natoms(ret.crystal)
        for cell in all_cells(ret)
            ret.interactions_union[cell, i] = clone_interactions(ints[i])
        end
    end

    return ret
end


"""
    enable_dipole_dipole!(sys::System)

Enables long-range dipole-dipole interactions,

```math
    -(Œº‚ÇÄ/4œÄ) ‚àë_{‚ü®ij‚ü©}  (3 (ùêå_j‚ãÖùê´ÃÇ_{ij})(ùêå_i‚ãÖùê´ÃÇ_{ij}) - ùêå_i‚ãÖùêå_j) / |ùê´_{ij}|^3
```

where the sum is over all pairs of spins (singly counted), including periodic
images, regularized using the Ewald summation convention. The magnetic moments
are ``ùêå_i = Œº_B g ùêí_i`` where ``g`` is the g-factor or g-tensor, and ``ùêí_i``
is the spin angular momentum dipole in units of ƒß. The Bohr magneton ``Œº_B`` and
vacuum permeability ``Œº_0`` are physical constants, with numerical values
determined by the unit system.
"""
function enable_dipole_dipole!(sys::System{N}) where N
    sys.ewald = Ewald(sys)
    return
end

"""
    set_external_field!(sys::System, B::Vec3)

Sets the external field `B` that couples to all spins.
"""
function set_external_field!(sys::System, B)
    for site in all_sites(sys)
        set_external_field_at!(sys, B, site)
    end
end

"""
    set_external_field_at!(sys::System, B::Vec3, site::Site)

Sets a Zeeman coupling between a field `B` and a single spin. [`Site`](@ref)
includes a unit cell and a sublattice index.
"""
function set_external_field_at!(sys::System, B, site)
    sys.extfield[to_cartesian(site)] = Vec3(B)
end

"""
    set_vacancy_at!(sys::System, site::Site)

Make a single site nonmagnetic. [`Site`](@ref) includes a unit cell and a
sublattice index.
"""
function set_vacancy_at!(sys::System{N}, site) where N
    is_homogeneous(sys) && error("Use `to_inhomogeneous` first.")

    site = to_cartesian(site)
    sys.Œ∫s[site] = 0.0
    sys.dipoles[site] = zero(Vec3)
    sys.coherents[site] = zero(CVec{N})
end


function local_energy_change(sys::System{N}, site, state::SpinState) where N
    (; s, Z) = state
    (; latsize, extfield, dipoles, coherents, ewald) = sys

    if is_homogeneous(sys)
        (; aniso, heisen, exchange, biquad) = interactions_homog(sys)[to_atom(site)]
    else
        (; aniso, heisen, exchange, biquad) = interactions_inhomog(sys)[site]
    end

    s‚ÇÄ = dipoles[site]
    Z‚ÇÄ = coherents[site]
    Œîs = s - s‚ÇÄ
    ŒîE = 0.0

    cell = to_cell(site)

    # Zeeman coupling to external field
    ŒîE -= sys.units.ŒºB * extfield[site] ‚ãÖ (sys.gs[site] * Œîs)

    # Single-ion anisotropy, dipole or SUN mode
    if N == 0
        E_new, _ = energy_and_gradient_for_classical_anisotropy(s, aniso.stvexp)
        E_old, _ = energy_and_gradient_for_classical_anisotropy(s‚ÇÄ, aniso.stvexp)
        ŒîE += E_new - E_old
    else
        Œõ = aniso.matrep
        ŒîE += real(dot(Z, Œõ, Z) - dot(Z‚ÇÄ, Œõ, Z‚ÇÄ))
    end

    # Heisenberg exchange
    for (; bond, J) in heisen
        s‚±º = dipoles[offsetc(cell, bond.n, latsize), bond.j]
        ŒîE += J * (Œîs ‚ãÖ s‚±º)    
    end

    # Quadratic exchange matrix
    for (; bond, J) in exchange
        s‚±º = dipoles[offsetc(cell, bond.n, latsize), bond.j]
        ŒîE += dot(Œîs, J, s‚±º)
    end

    # Scalar biquadratic exchange
    for (; bond, J) in biquad
        cell‚±º = offsetc(cell, bond.n, latsize)
        s‚±º = dipoles[cell‚±º, bond.j]
        if sys.mode == :dipole
            # Renormalization introduces a factor r and a Heisenberg term
            S·µ¢ = (sys.Ns[site]-1)/2
            S‚±º = (sys.Ns[cell‚±º, bond.j]-1)/2
            S = ‚àö(S·µ¢*S‚±º)
            r = (1 - 1/S + 1/4S^2)
            ŒîE += J * (r*((s‚ãÖs‚±º)^2 - (s‚ÇÄ‚ãÖs‚±º)^2) - (Œîs‚ãÖs‚±º)/2)
        elseif sys.mode == :large_S
            ŒîE += J * ((s‚ãÖs‚±º)^2 - (s‚ÇÄ‚ãÖs‚±º)^2)
        elseif sys.mode == :SUN
            error("Biquadratic currently unsupported in SU(N) mode.") 
        end
    end

    # Long-range dipole-dipole
    if !isnothing(ewald)
        ŒîE += ewald_energy_delta(sys, site, s)
    end

    return ŒîE
end


"""
    energy(sys::System)

Computes the total system energy.
"""
function energy(sys::System{N}) where N
    (; crystal, latsize, dipoles, extfield, ewald) = sys

    E = 0.0

    # Zeeman coupling to external field
    for site in all_sites(sys)
        E -= sys.units.ŒºB * extfield[site] ‚ãÖ (sys.gs[site] * dipoles[site])
    end

    # Anisotropies and exchange interactions
    for i in 1:natoms(crystal)
        if is_homogeneous(sys)
            ints = interactions_homog(sys)
            E += energy_aux(sys, ints[i], i, all_cells(sys), homog_bond_iterator(latsize))
        else
            for cell in all_cells(sys)
                ints = interactions_inhomog(sys)
                E += energy_aux(sys, ints[cell, i], i, (cell,), inhomog_bond_iterator(latsize, cell))
            end
        end
    end

    # Long-range dipole-dipole
    if !isnothing(ewald)
        E += ewald_energy(sys)
    end
    
    return E
end

# Total energy contributed by sublattice `i`, summed over the list of `cells`.
# The function `foreachbond` enables efficient iteration over neighboring cell
# pairs.
function energy_aux(sys::System{N}, ints::Interactions, i::Int, cells, foreachbond) where N
    (; dipoles, coherents, latsize) = sys
    E = 0.0

    # Single-ion anisotropy
    if N == 0       # Dipole mode
        for cell in cells
            s = dipoles[cell, i]
            E += energy_and_gradient_for_classical_anisotropy(s, ints.aniso.stvexp)[1]
        end
    else            # SU(N) mode
        for cell in cells
            Œõ = ints.aniso.matrep
            Z = coherents[cell, i]
            E += real(dot(Z, Œõ, Z))
        end
    end

    # Heisenberg exchange
    foreachbond(ints.heisen) do J, site1, site2
        s·µ¢ = dipoles[site1]
        s‚±º = dipoles[site2]
        E += J * dot(s·µ¢, s‚±º)
    end

    # Quadratic exchange matrix
    foreachbond(ints.exchange) do J, site1, site2
        s·µ¢ = dipoles[site1]
        s‚±º = dipoles[site2]
        E += dot(s·µ¢, J, s‚±º)
    end

    # Scalar biquadratic exchange
    foreachbond(ints.biquad) do J, site1, site2
        s·µ¢ = dipoles[site1]
        s‚±º = dipoles[site2]
        if sys.mode == :dipole
            # Renormalization introduces a factor r and a Heisenberg term
            S·µ¢ = (sys.Ns[site1]-1)/2
            S‚±º = (sys.Ns[site2]-1)/2
            S = ‚àö(S·µ¢*S‚±º)
            r = (1 - 1/S + 1/4S^2)
            E += J * (r*(s·µ¢‚ãÖs‚±º)^2 - (s·µ¢‚ãÖs‚±º)/2 + S^3 + S^2/4)
        elseif sys.mode == :large_S
            E += J * (s·µ¢‚ãÖs‚±º)^2
        elseif sys.mode == :SUN
            error("Biquadratic currently unsupported in SU(N) mode.")
        end
    end

    return E
end

# Updates B in-place to hold negative energy gradient, -dE/ds, for each spin.
function set_forces!(B, dipoles::Array{Vec3, 4}, sys::System{N}) where N
    (; crystal, latsize, extfield, ewald) = sys

    fill!(B, zero(Vec3))

    # Zeeman coupling
    for site in all_sites(sys)
        B[site] += sys.units.ŒºB * (sys.gs[site]' * extfield[site])
    end

    # Anisotropies and exchange interactions
    for i in 1:natoms(crystal)
        if is_homogeneous(sys)
            # Interaction is the same at every cell
            interaction = sys.interactions_union[i]
            set_forces_aux!(B, dipoles, interaction, sys, i, all_cells(sys), homog_bond_iterator(latsize))
        else
            for cell in all_cells(sys)
                # There is a different interaction at every cell
                interaction = sys.interactions_union[cell,i]
                set_forces_aux!(B, dipoles, interaction, sys, i, (cell,), inhomog_bond_iterator(latsize, cell))
            end
        end
    end

    if !isnothing(ewald)
        accum_ewald_force!(B, dipoles, sys)
    end
end

# Calculate the force `B' for the sublattice `i' at all elements of `cells`. The
# function `foreachbond` enables efficient iteration over neighboring cell
# pairs.
function set_forces_aux!(B, dipoles::Array{Vec3, 4}, ints::Interactions, sys::System{N}, i::Int, cells, foreachbond) where N
    # Single-ion anisotropy only contributes in dipole mode. In SU(N) mode, the
    # anisotropy matrix will be incorporated directly into ‚Ñå.
    if N == 0
        for cell in cells
            s = dipoles[cell, i]
            B[cell, i] -= energy_and_gradient_for_classical_anisotropy(s, ints.aniso.stvexp)[2]
        end
    end

    # Heisenberg exchange
    foreachbond(ints.heisen) do J, site1, site2
        s·µ¢ = dipoles[site1]
        s‚±º = dipoles[site2]
        B[site1] -= J  * s‚±º
        B[site2] -= J' * s·µ¢
    end

    # Quadratic exchange matrix
    foreachbond(ints.exchange) do J, site1, site2
        s·µ¢ = dipoles[site1]
        s‚±º = dipoles[site2]
        B[site1] -= J  * s‚±º
        B[site2] -= J' * s·µ¢
    end

    # Scalar biquadratic exchange
    foreachbond(ints.biquad) do J, site1, site2
        s·µ¢ = dipoles[site1]
        s‚±º = dipoles[site2]

        if sys.mode == :dipole
            S·µ¢ = (sys.Ns[site1]-1)/2
            S‚±º = (sys.Ns[site2]-1)/2
            S = ‚àö(S·µ¢*S‚±º)
            # Renormalization introduces a factor r and a Heisenberg term
            r = (1 - 1/S + 1/4S^2)
            B[site1] -= J * (2r*s‚±º*(s·µ¢‚ãÖs‚±º) - s‚±º/2)
            B[site2] -= J * (2r*s·µ¢*(s·µ¢‚ãÖs‚±º) - s·µ¢/2)
        elseif sys.mode == :large_S
            B[site1] -= J * 2s‚±º*(s·µ¢‚ãÖs‚±º)
            B[site2] -= J * 2s·µ¢*(s·µ¢‚ãÖs‚±º)
        elseif sys.mode == :SUN
            error("Biquadratic currently unsupported in SU(N) mode.")
        end
    end
end

# Producer of a functions that iterate over all interactions for a given cell
function inhomog_bond_iterator(latsize, cell)
    return function inner(f, ints)
        for (; isculled, bond, J) in ints
            # Early return to avoid double-counting a bond
            isculled && break

            # Neighboring cell may wrap the system
            cell‚Ä≤ = offsetc(cell, bond.n, latsize)
            f(J, CartesianIndex(cell, bond.i), CartesianIndex(cell‚Ä≤, bond.j))
        end
    end
end

# Producer of a functions that iterate over all interactions, involving all
# pairs of cells in a homogeneous system
function homog_bond_iterator(latsize)
    return function inner(f, ints)
        for (; isculled, bond, J) in ints
            # Early return to avoid double-counting a bond
            isculled && break

            # Iterate over all cells and periodically shifted neighbors
            for (ci, cj) in zip(CartesianIndices(latsize), CartesianIndicesShifted(latsize, bond.n.data)) # kbtodo: Tuple(bond.n)
                f(J, CartesianIndex(ci, bond.i), CartesianIndex(cj, bond.j))
            end
        end
    end
end

"""
    forces(Array{Vec3}, sys::System)

Returns the effective local field (force) at each site, ``ùêÅ = -‚àÇE/‚àÇùê¨``.
"""
function forces(sys::System{N}) where N
    B = zero(sys.dipoles)
    set_forces!(B, sys.dipoles, sys)
    return B
end
