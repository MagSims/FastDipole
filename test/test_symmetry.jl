@testitem "Crystal Construction" begin
    include("test_shared.jl")


    ### Test construction of diamond lattice

    # Spglib inferred symmetry
    lat_vecs = [1 1 0; 1 0 1; 0 1 1]' / 2
    positions = [[1, 1, 1], [-1, -1, -1]] / 8
    cryst = Crystal(lat_vecs, positions)
    ref_bonds = reference_bonds(cryst, 2.)
    dist1 = [distance(cryst, b) for b in ref_bonds]

    # Using explicit symops
    lat_vecs = Sunny.Mat3(lat_vecs)
    positions = [Sunny.Vec3(1, 1, 1) / 8]
    types = [""]
    cryst = Sunny.crystal_from_symops(lat_vecs, positions, types, cryst.symops, cryst.spacegroup)
    ref_bonds = reference_bonds(cryst, 2.)
    dist2 = [distance(cryst, b) for b in ref_bonds]

    # Using Hall number
    lat_vecs = lattice_vectors(1, 1, 1, 90, 90, 90) # must switch to standard cubic unit cell
    positions = [Sunny.Vec3(1, 1, 1) / 4]
    cryst = Sunny.crystal_from_hall_number(lat_vecs, positions, types, 525)
    ref_bonds = reference_bonds(cryst, 2.)
    dist3 = [distance(cryst, b) for b in ref_bonds]

    # Using international symbol
    positions = [[1, 1, 1] / 4]
    # cryst = Crystal(lat_vecs, positions, "F d -3 m") # Ambiguous!
    cryst = Crystal(lat_vecs, positions, "F d -3 m"; setting="1")
    ref_bonds = reference_bonds(cryst, 2.)
    dist4 = [distance(cryst, b) for b in ref_bonds]

    @test dist1 ‚âà dist2 ‚âà dist3 ‚âà dist4



    ### FCC lattice, primitive vs. standard unit cell

    lat_vecs = [1 1 0; 1 0 1; 0 1 1]' / 2
    positions = [[0, 0, 0]]
    cryst = Crystal(lat_vecs, positions)

    lat_vecs = [1 0 0; 0 1 0; 0 0 1]'
    positions = [[0, 0, 0], [0.5, 0.5, 0], [0.5, 0, 0.5], [0, 0.5, 0.5]]
    cryst‚Ä≤ = Crystal(lat_vecs, positions)

    @test cryst.sitesyms[1] == cryst‚Ä≤.sitesyms[1]

    # Calculate interaction table
    ref_bonds = reference_bonds(cryst, 2.)
    b = ref_bonds[2]
    basis = Sunny.basis_for_symmetry_allowed_couplings(cryst, b)
    J = basis' * randn(length(basis))
    (bs, Js) = all_symmetry_related_couplings_for_atom(cryst, b.i, b, J)
    @test length(Js) == coordination_number(cryst, b.i, b)


    ### Triangular lattice, primitive unit cell

    c = 10
    lat_vecs = [1 0 0;  -1/2 ‚àö3/2 0;  0 0 c]'
    positions = [[0, 0, 0]]
    cryst = Crystal(lat_vecs, positions)
    @test cell_type(cryst) == Sunny.hexagonal
    @test nbasis(cryst) == 1
    @test cell_volume(cryst) ‚âà c * ‚àö3 / 2 
    @test all(lattice_params(cryst) .‚âà (1., 1., c, 90., 90., 120.))

    ### Kagome lattice

    lat_vecs = [1 0 0;  -1/2 ‚àö3/2 0;  0 0 c]'
    positions = [[0, 0, 0], [0.5, 0, 0], [0, 0.5, 0]]
    cryst = Crystal(lat_vecs, positions)
    @test cell_type(cryst) == Sunny.hexagonal
    @test nbasis(cryst) == 3
    @test cell_volume(cryst) ‚âà c * ‚àö3 / 2 
    @test all(lattice_params(cryst) .‚âà (1., 1., c, 90., 90., 120.))


    ### Arbitrary monoclinic

    mono_lat_params = (6, 7, 8, 90, 90, 40)
    lat_vecs = lattice_vectors(mono_lat_params...)
    positions = [[0,0,0]]
    # cryst = Crystal(lat_vecs, positions, "C 2/c")
    cryst = Crystal(lat_vecs, positions, "C 2/c", setting="c1")
    @test cell_type(cryst) == Sunny.monoclinic
    @test nbasis(cryst) == 4
    @test all(lattice_params(cryst) .‚âà mono_lat_params)


    ### Arbitrary trigonal

    lat_vecs = lattice_vectors(5, 5, 6, 90, 90, 120)
    positions = [[0,0,0]]
    cryst1 = Crystal(lat_vecs, positions, "P -3")
    @test nbasis(cryst1) == 1
    @test cell_type(cryst1) == Sunny.hexagonal
    cryst2 = Crystal(lat_vecs, positions, "R -3")
    @test nbasis(cryst2) == 3
    cryst3 = Crystal(lat_vecs, positions, 147) # spacegroup number
    @test cell_type(cryst1) == cell_type(cryst2) == cell_type(cryst3) == Sunny.hexagonal


    ### Arbitrary triclinic

    lat_vecs = lattice_vectors(6, 7, 8, 70, 80, 90)
    positions = [[0,0,0]]
    cryst1 = Crystal(lat_vecs, positions, "P 1")
    @test nbasis(cryst1) == 1
    cryst2 = Crystal(lat_vecs, positions) # Infers 'P -1'
    @test nbasis(cryst1) == nbasis(cryst2) == 1
    @test cell_type(cryst1) == cell_type(cryst2) == Sunny.triclinic

    ### Orthorhombic test, found by Ovi Garlea

    lat_vecs = lattice_vectors(13.261, 7.718, 6.278, 90.0, 90.0, 90.0);
    types = ["Yb1","Yb2"];
    basis_vecs = [[0,0,0], [0.266,0.25,0.02]]; # Locations of atoms as multiples of lattice vectors
    crysts = Crystal(lat_vecs, basis_vecs, 62; types, symprec=1e-4)
    @test length(crysts) == 6
    cryst = Crystal(lat_vecs, basis_vecs,62; types, symprec=1e-4, setting="-cba")
    @test count(==(1), cryst.classes) == 4
    @test count(==(2), cryst.classes) == 4    
end


@testitem "Spin matrices" begin
    include("test_shared.jl")
    
    # Levi-Civita symbol
    œµ = [(i-j)*(j-k)*(k-i)/2 for i=1:3, j=1:3, k=1:3]

    # Kronecker delta
    Œ¥(i,j) = (i==j) ? 1 : 0

    ### Verify ùî∞ùî≤(2) irreps
    for N = 2:5
        S‚ÇÄ = (N-1)/2
        S = Sunny.spin_matrices(N)

        for i=1:3, j=1:3
            # Test commutation relations
            @test S[i]*S[j] - S[j]*S[i] ‚âà im * sum(œµ[i,j,k]*S[k] for k=1:3)

            # Test orthonormality
            @test tr(S[i]*S[j]) ‚âà (2/3)*S‚ÇÄ*(S‚ÇÄ+1/2)*(S‚ÇÄ+1)*Œ¥(i,j)
        end

        # Test magnitude
        @test sum(S[i]^2 for i=1:3) ‚âà S‚ÇÄ*(S‚ÇÄ+1)*I

        # Test dipole -> ket -> dipole round trip
        n = S‚ÇÄ * normalize(randn(Sunny.Vec3))
        œà = Sunny._get_coherent_from_dipole(n, Val(N))
        @test Sunny.expected_spin(œà) ‚âà n
    end    
end

@testitem "Sparse B‚ãÖùêí" begin
    # Test that action `accum_spin_matrices!(acc, B)` is identical to adding B‚ãÖùêí to `acc`
    for N = 4:6
        acc = zeros(ComplexF64, N, N)
        B = randn(Sunny.Vec3)
        Sunny.accum_spin_matrices!(acc, B)
        @test acc ‚âà sum(Sunny.spin_matrices(N) .* B)
    end
end

@testitem "Spherical tensors" begin
    include("test_shared.jl")

    # Spherical tensors that satisfy `norm(T) =  ‚àö tr T‚Ä† T = 1`.
    function spherical_tensors_normalized(k; N)
        S = (N-1)/2
        ret = Matrix{Float64}[]
        for q = k:-1:-k
            T = zeros(Float64, N, N)
            for i = 1:N, i‚Ä≤ = 1:N
                m  = S - i + 1
                m‚Ä≤ = S - i‚Ä≤+ 1
                T[i, i‚Ä≤] = clebschgordan(S, m‚Ä≤, k, q, S, m) * sqrt((2k+1)/N)
            end
            push!(ret, T)
        end
        return ret
    end

    # Spherical tensors T(k,q) as NxN matrices. The result is ambiguous up to an
    # overall (k,N)-dependent scaling factor. Here we're using the normalization
    # convention of KS/BCS.
    function spherical_tensors(k; N)
        j = (N-1)/2
        ret = Matrix{Float64}[]
        for q = k:-1:-k
            Tq = zeros(Float64, N, N)
            for i‚Ä≤ = 1:N, i = 1:N
                m‚Ä≤ = j - i‚Ä≤+ 1
                m  = j - i + 1

                # By the Wigner-Eckhardt theorem, the spherical tensor T must have
                # this m and m‚Ä≤ dependence. An overall (j, k)-dependent rescaling
                # factor is arbitrary, however.
                Tq[i‚Ä≤, i] = (-1)^(j-m‚Ä≤) * wigner3j(j, k, j, -m‚Ä≤, q, m)
            end

            # Below we will apply two rescaling factors obtained from Rudowicz and
            # Chung, J. Phys.: Condens. Matter 16 (2004) 5825‚Äì5847.

            # With this rescaling factor, we get the Buckmaster and Smith & Thornley
            # (BST) operator
            Tq .*= 2.0^(-k) * sqrt(factorial((N-1)+k+1) / factorial((N-1)-k))

            # With this additional rescaling factor, we get the Koster and Statz
            # (1959) and Buckmaster et al (1972) operator (KS/BCS)
            Tq ./= sqrt(factorial(2k) / (2^k * factorial(k)^2))

            push!(ret, Tq)
        end
        return ret
    end

    # Lie bracket, aka matrix commutator
    bracket(A, B) = A*B - B*A

    for N=2:7
        S = Sunny.spin_matrices(N)
        Sp = S[1] + im*S[2]
        Sm = S[1] - im*S[2]
        
        for k = 0:N-1
            # Spherical tensors acting on N-dimensional Hilbert space
            T = spherical_tensors(k; N)

            # Generators of rotations in the spin-k representation
            K = Sunny.spin_matrices(2k+1)

            # The selected basis is q ‚àà [|k‚ü©, |k-1‚ü©, ... |-k‚ü©]. This function
            # converts from a q value to a 1-based index.
            idx(q) = k-q+1

            # A random axis-angle
            Œ∏ = randn(3)
            # Different representations of the same physical rotation
            D = exp(-im * Œ∏' * K)
            U = exp(-im * Œ∏' * S)

            for q = -k:k
                # Racah's commutation relations
                @test bracket(S[3], T[idx(q)]) ‚âà q * T[idx(q)]
                q < +k && @test bracket(Sp, T[idx(q)]) ‚âà sqrt((k-q)*(k+q+1)) * T[idx(q+1)]
                q > -k && @test bracket(Sm, T[idx(q)]) ‚âà sqrt((k+q)*(k-q+1)) * T[idx(q-1)]

                # Wigner D matrix encodes rotation
                @test U' * T[idx(q)] * U ‚âà (conj(D) * T)[idx(q)]
            end
        end
    end

    # Stevens operators
    for N=2:7
        for k = 1:N-1
            ùí™ = Sunny.stevens_matrices(k; N)
            T = spherical_tensors(k; N)

            # Check that Stevens operators are proper linear combination of
            # spherical tensors
            @test ùí™ ‚âà Sunny.stevens_Œ±[k] * T
    
            # Check conversion of coefficients
            c = randn(2k+1)
            b = Sunny.transform_spherical_to_stevens_coefficients(k, c)
            @test transpose(c)*T ‚âà transpose(b)*ùí™
        end
    end
end

@testitem "Local operator symbols" begin
    include("test_shared.jl")

    A = randn(3,3)
    R = Sunny.Mat3(exp(A - A'))
    N = 5

    # Test axis-angle decomposition
    let
        (n, Œ∏) = Sunny.axis_angle(R)
        @test 1 + 2cos(Œ∏) ‚âà tr(R)
        @test norm(n) ‚âà 1
        @test R*n ‚âà n

        # Rodrigues formula
        Œ¥(i,j) = (i==j) ? 1 : 0
        œµ(i,j,k) = (i-j)*(j-k)*(k-i)/2
        R2 = zeros(3,3)
        for i=1:3, j=1:3
            R2[i,j] = Œ¥(i,j)*cos(Œ∏) + (1-cos(Œ∏))*n[i]*n[j] - sin(Œ∏)*sum(œµ(i,j,k)*n[k] for k=1:3)
        end
        @test R2 ‚âà R
    end

    # Test that Stevens operator symbols transform properly
    let
        p = randn(3)' * Sunny.stevens_operator_symbols[1] +
            randn(5)' * Sunny.stevens_operator_symbols[2] +
            randn(7)' * Sunny.stevens_operator_symbols[3]
        @test Sunny.operator_to_matrix(rotate_operator(p, R); N) ‚âà rotate_operator(Sunny.operator_to_matrix(p; N), R)
    end

    # Test that spin operator symbols transform properly
    let
        J = randn(3, 3)
        J = (J+J')/2
        p = randn(3)'*ùíÆ + ùíÆ'*J*ùíÆ
        @test Sunny.operator_to_matrix(rotate_operator(p, R); N) ‚âà rotate_operator(Sunny.operator_to_matrix(p; N), R)
    end

    # Test that a linear combination transforms properly
    let
        p = randn(3)'*ùíÆ + randn(5)'*Sunny.stevens_operator_symbols[2]
        @test Sunny.operator_to_matrix(rotate_operator(p, R); N) ‚âà rotate_operator(Sunny.operator_to_matrix(p; N), R)
    end

    # Internal conversion between spin and Stevens operators
    let
        J = randn(3,3)
        J = J+J'
        p = randn(3)'*ùíÆ + ùíÆ'*J*ùíÆ +
            randn(11)' * Sunny.stevens_operator_symbols[5] +
            randn(13)' * Sunny.stevens_operator_symbols[6]
        cp1 = p |> Sunny.operator_to_classical_polynomial
        cp2 = p |> Sunny.operator_to_classical_stevens |> Sunny.operator_to_classical_polynomial
        @test cp1 ‚âà cp2
    end

    # Test some inferred anisotropy matrices
    let
        N = 7
        k = 6
        i = 1
        cryst = Sunny.diamond_crystal()

        # print_site(cryst, i)
        Œõ = ùí™[6,0]-21ùí™[6,4]
        @test Sunny.is_anisotropy_valid(cryst, i, Œõ)

        R = [normalize([1 1 -2]); normalize([-1 1 0]); normalize([1 1 1])]
        # print_site(cryst, i; R)
        Œõ = ùí™[6,0]-(35/‚àö8)*ùí™[6,3]+(77/8)*ùí™[6,6]
        Œõ‚Ä≤ = rotate_operator(Œõ, R)
        @test Sunny.is_anisotropy_valid(cryst, i, Œõ‚Ä≤)

        lat_vecs = lattice_vectors(1.0, 1.1, 1.0, 90, 90, 90)
        cryst = Crystal(lat_vecs, [[0., 0., 0.]])
        # print_site(cryst, i)
        Œõ = randn()*(ùí™[6,0]-21ùí™[6,4]) + randn()*(ùí™[6,2]+(16/5)*ùí™[6,4]+(11/5)*ùí™[6,6])
        @test Sunny.is_anisotropy_valid(cryst, i, Œõ)
    end

    # Test fast evaluation of Stevens operators
    let
        import DynamicPolynomials as DP

        s = randn(Sunny.Vec3)
        p = randn(5)' * Sunny.stevens_operator_symbols[2] + 
            randn(9)' * Sunny.stevens_operator_symbols[4] +
            randn(13)' * Sunny.stevens_operator_symbols[6]
        (_, c2, _, c4, _, c6) = Sunny.operator_to_classical_stevens_coefficients(p, 1.0)

        p_classical = Sunny.operator_to_classical_polynomial(p)
        grad_p_classical = DP.differentiate(p_classical, Sunny.spin_classical_symbols)

        E_ref = p_classical(Sunny.spin_classical_symbols => s)
        gradE_ref = [g(Sunny.spin_classical_symbols => s) for g = grad_p_classical]

        E, gradE = Sunny.energy_and_gradient_for_classical_anisotropy(s, c2, c4, c6)

        @test E ‚âà E_ref

        # Above, when calculating gradE_ref, the value X = |S|^2 is treated
        # as varying with S, such that dX/dS = 2S. Conversely, when calculating
        # gradE, the value X is treated as a constant, such that dX/dS = 0. In
        # practice, gradE will be used to drive spin dynamics, for which |S| is
        # constant, and the component of gradE parallel to S will be projected
        # out anyway. Therefore we only need agreement between the parts of
        # gradE and gradE_ref that are perpendicular to S.
        gradE_ref -= (gradE_ref‚ãÖs)*s / (s‚ãÖs) # Orthogonalize to s
        gradE -= (gradE‚ãÖs)*s / (s‚ãÖs)         # Orthogonalize to s
        @test gradE_ref ‚âà gradE

    end

    # Test that when operators rotate contravariant to kets, expectation values
    # are invariant (scalar)
    let
        # Dimension N unitary transformation for R
        N = 5
        U = Sunny.unitary_for_rotation(R; N)

        # Random spins
        z = normalize(randn(ComplexF64, N))
        s = normalize(randn(3))

        # Two random operators
        p1 = randn(3)' * Sunny.stevens_operator_symbols[1]
        p2 = randn(3)' * ùíÆ
        for p = [p1, p2]
            # Inner products are invariant when operators are rotated conversely to kets
            p_rot = rotate_operator(p, R')
            z_rot = U*z
            Œõ = Sunny.operator_to_matrix(p; N)
            Œõ_rot = Sunny.operator_to_matrix(p_rot; N)
            @test z'*Œõ*z ‚âà z_rot'*Œõ_rot*z_rot

            # Same thing, but with spin dipoles
            q = Sunny.operator_to_classical_polynomial(p)
            q_rot = Sunny.operator_to_classical_polynomial(p_rot)
            @test q(Sunny.spin_classical_symbols => s) ‚âà q_rot(Sunny.spin_classical_symbols => R*s)
        end
    end

    # Test validity of symmetry inferred anisotropies 
    let
        latvecs = [1 0 0; 0 1 0; 0 0 10]'
        basis = [[0.1, 0, 0], [0, 0.1, 0], [0.9, 0, 0], [0, 0.9, 0]]
        cryst = Crystal(latvecs, basis)

        # Most general allowed anisotropy for this crystal
        Œõ = randn(9)'*[ùí™[2,0], ùí™[2,2], ùí™[4,0], ùí™[4,2], ùí™[4,4], ùí™[6,0], ùí™[6,2], ùí™[6,4], ùí™[6,6]]

        # Test anisotropy invariance in "dipole-mode"
        N = 0
        sys = SpinSystem(cryst, [anisotropy(Œõ, 1)], (1,1,1), [SiteInfo(1; N)])
        rand!(sys)
        E1 = energy(sys)
        # Effectively rotate site positions by œÄ/2 clockwise
        sys._dipoles .= circshift(sys._dipoles, (0,0,0,1))
        # Rotate spin vectors correspondingly
        R = Sunny.Mat3([0 1 0; -1 0 0; 0 0 1])
        sys._dipoles .= [R*d for d in sys._dipoles]
        E2 = energy(sys)
        @test E1 ‚âà E2

        # Test anisotropy invariance in "SU(N)-mode"
        N = 5
        sys = SpinSystem(cryst, [anisotropy(Œõ, 1)], (1,1,1), [SiteInfo(1; N)])
        rand!(sys)
        E1 = energy(sys)
        # Effectively rotate site positions by œÄ/2 clockwise
        sys._coherents .= circshift(sys._coherents, (0,0,0,1))
        # Rotate kets correspondingly
        U = Sunny.unitary_for_rotation(R; N)
        sys._coherents .= [U*z for z in sys._coherents]
        E2 = energy(sys)
        @test E1 ‚âà E2
    end
end
